import io.fabric.gradle.weaving.ArtifactSizePlugin

/*
 * Copyright (C) 2015 Twitter, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */

apply plugin: 'com.android.library'
apply from: rootProject.qualityScript
apply plugin: ArtifactSizePlugin

android {
    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion

    defaultConfig {
        versionCode 1
        versionName version
        group pom_group_id

        minSdkVersion rootProject.minSdkVersion
    }

    lintOptions {
        warningsAsErrors true
    }

    buildTypes {
        debug {
            buildConfigField "String", "GROUP", "\"${pom_group_id}\""
            buildConfigField "String", "ARTIFACT_ID", "\"${pom_artifact_id}\""
            buildConfigField "String", "BUILD_NUMBER", "\"${buildNumber}\""

            testCoverageEnabled enableTestCodeCoverage
        }

        release {
            buildConfigField "String", "GROUP", "\"${pom_group_id}\""
            buildConfigField "String", "ARTIFACT_ID", "\"${pom_artifact_id}\""
            buildConfigField "String", "BUILD_NUMBER", "\"${buildNumber}\""
        }
    }

    //Gradle publishes only release by default for multi module projects
    //This will publish all build variants and we can specifically choose them
    defaultPublishConfig 'release'
    publishNonDefault true

    compileOptions {
        sourceCompatibility rootProject.sourceCompatibilityVersion
        targetCompatibility rootProject.targetCompatibilityVersion
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }
}

ext.projectDependencies = [
        ':sdk:twitter:twitter-core',
        ':sdk:twitter:tweet-ui',
        ':sdk:twitter:tweet-composer'
]

dependencies {
    projectDependencies.each { path ->
        api project(path: path)
    }

    testCompile dependency.mockito
    testCompile dependency.junit
    testCompile(dependency.robolectric) {
        exclude group: 'commons-logging', module: 'commons-logging'
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    }
}

apply from: rootProject.deployScript
